#!ipxe

# Microsoft Windows
# https://www.microsoft.com

cpuid --ext 29 && set arch x64 || set arch x86
set win_image Windows_11_24H2
set win_base_url https://boot.lucadev.de
set unattended_key disabled
goto ${menu} ||

:windows
clear win_version
menu ${os}
item --gap Installers
item win_install ${space} Load ${win_image} Installer...
item --gap Options:
item image_set ${space} Image [ ${win_image} ]
item unattended_key_set ${space} Unattended Installation [ ${unattended_key} ]
choose win_version || goto windows_exit
goto ${win_version}

:image_set
menu Image
{{WINVERSIONS}}
choose win_image && goto windows

:win_install
isset ${win_base_url} && goto boot || echo URL not set... && goto url_set

:unattended_key_set
menu Unattended Installation
item disabled Disabled
item ask Ask for Key
{{UNATTENDED_KEYS}}
choose unattended_key && goto windows

:boot
imgfree
kernel http://${boot_domain}/wimboot
iseq ${unattended_key} disabled || initrd -n autounattend.xml ${win_base_url}/unattended?key=${unattended_key:uristring} autounattended.xml
initrd -n winpeshl.ini       ${win_base_url}/winpeshl winpeshl.ini
initrd -n configure.exe       ${win_base_url}/configure.exe configure.exe
initrd -n bootmgr            ${win_base_url}/WinPE/${arch}/bootmgr       bootmgr ||
initrd -n bootmgr.efi        ${win_base_url}/WinPE/${arch}/bootmgr.efi   bootmgr.efi ||
initrd -n bcd                ${win_base_url}/WinPE/${arch}/boot/bcd      bcd ||
initrd -n bcd                ${win_base_url}/WinPE/${arch}/Boot/BCD      bcd ||
initrd -n boot.sdi           ${win_base_url}/WinPE/${arch}/boot/boot.sdi boot.sdi ||
initrd -n boot.sdi           ${win_base_url}/WinPE/${arch}/Boot/boot.sdi boot.sdi ||
initrd -n boot.wim           ${win_base_url}/WinPE/${arch}/sources/boot.wim boot.wim
boot

:windows_exit
exit 0
